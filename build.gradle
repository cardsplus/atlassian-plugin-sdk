/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 */

buildscript {
    ext {
        /* Docker-Image mit Atlassian Plugin SDK */
        PLUGIN_SDK_TAG = 'plugin-sdk:latest'
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
}

plugins {
    id "com.palantir.git-version" version "0.11.0"
    id 'base'
}

description = 'Atlassian Plugin-SDK'
version = gitVersion();

println ''
println '###############################################'
println "# ${description} ${version}"
println '###############################################'
println 'PLUGIN_SDK_TAG=' + "${PLUGIN_SDK_TAG}"
println ''

import org.apache.tools.ant.filters.*;

task assembleDockerfile(type: Copy) {
    assemble.dependsOn it
    from 'src/main/docker'
    include "Dockerfile"
    include "*.sh"
    // CRLF causes `/entrypoint.sh: not found`
    filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("lf"))
    into "${buildDir}/docker"
}

task image {
    mustRunAfter clean
    doLast {
        exec {
            commandLine 'docker', 'build', '-t', "${PLUGIN_SDK_TAG}", "${buildDir}/docker"
        }
    }
}

task smoke {
    mustRunAfter clean
    dependsOn image
    doLast {
        /* check icon file generation. */
        delete "${buildDir}/1"
        mkdir "${buildDir}/1"
        exec {
            commandLine 'docker', 'run', '--rm', '--entrypoint', '/entrypoint-test.sh', "${PLUGIN_SDK_TAG}"
        }
    }
}