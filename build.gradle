/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 */

buildscript {
    ext {
        /* Docker-Image mit Atlassian Plugin SDK */
        PLUGIN_SDK_TAG = 'atlassian-plugin-sdk:latest'
    }
}

plugins {
    id 'base'
    id 'com.palantir.git-version' version '3.2.0'
}

description = 'Atlassian plugin SDK'
version = gitVersion();

import org.apache.tools.ant.filters.*;

task assembleDocker(type: Copy) {
    assemble.dependsOn it
    from 'src/main/docker'
    include "Dockerfile"
    include "*.sh"
    // CRLF causes `/entrypoint.sh: not found`
    filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance("lf"))
    into "${buildDir}/docker"
}

task image {
    mustRunAfter clean    
    dependsOn assemble
    doLast {
        exec {
            commandLine 'docker', 'build', '-t', "${PLUGIN_SDK_TAG}", "${buildDir}/docker"
        }
    }
}

task smoke {
    mustRunAfter clean
    dependsOn image
    doLast {
        exec {
            commandLine 'docker', 'run', '--rm', '--entrypoint', '/entrypoint.sh', "${PLUGIN_SDK_TAG}", "atlas-version"
        }
    }
}